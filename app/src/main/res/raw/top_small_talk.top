topic: ~top_small_talk()

concept:(hello) [hi hallo hey "guten [Morgen Tag Abend]"]

concept:(good) [gut "sehr gut" super bestens blendend]
concept:(bad) [schlecht "nicht {so} gut" "geht so"]

concept:(party) ["ich war mit [Freunden "meiner Familie" Bekannten] essen"
                 "ich habe mich mit [Freunden "meiner Familie" Bekannten] getroffen"]

concept:(tired) [schlapp müde kraftlos fertig]

concept:(pain) ["[Kopf Bauch Arm Bein Brust Rücken Knie Fuss] Schmerzen" "mir tut ["der Kopf" "der Bauch" "der Arm" "das Bein" "die Brust" "der Rücken" "das Knie" "der Fuss"] weh" "ich habe Schmerzen" "ich habe [Kopf Bauch] weh"]

concept:(female) [die meine eine Tochter Tante Schwester Nachbarin Mitbewohnerin Pflegerin Ärztin]
concept:(male) [den mein einen ein meinen Sohn Onkel Bruder Nachbarn Mitbwohner Pfleger Arzt]

concept:(time) ["Wie viel Uhr ist es?" "Wie viel Uhr haben wir?" "Was haben wir für eine Uhrzeit?" "Wie spät ist es?"]
concept:(date) [Datum "Was ist heute für ein Tag?" "Was [ist "haben wir"] heute für einen Tag?" "Welches Datum [ist "haben wir"] heute?"]

concept:(yes) [ja gerne sicherlich "auf jeden Fall" "ich bitte darum" bitte]
concept:(no) [nein danke "lieber nicht" nö "keine Lust"]

concept:(plans)["ich plane" "wir planen" "ich will" "wir wollen" "ich habe vor" "wir haben vor" geplant "ist geplant" "ich gedenke" später "ich gehe" "wir gehen" "Kaffee trinken"]

u:(hallo) Hallo ^enableThenGoto(START)

proposal:%START
    $talk = "Hallo Mensch"
    Hallo Mensch!
    u1:(~hello) ^execute(hello)
    $talk = "Wie geht es dir?"
    ^rand[
        "Wie geht es dir?"
        "Wie schön dich zu sehen! Wie geht es dir?"
        "Wie schön dich zu sehen! Wie ist es dir heute?"
        "Ich freu mich dich zu sehen! Wie gehts dir?"
    ]
        u2:(~good)
            ^execute(nice)
            Prima! ^enableThenGoto(QUESTION)
        u2:(~bad)
            Das hört sich aber nicht so gut an! ^enableThenGoto(WHATSUP)

proposal:%WHATSUP
$talk = "Was hast du denn, Mensch?"
^rand[
    "Was ist denn los?"
    "Was hast du denn?"
    "Was fehlt dir?"
    "Was tut dir weh?"
]
    u1:(nichts) Okay ^endDiscuss(badMood)
    u1:("ich weiß es nicht" "keine Ahnung") $talk = "" Oh, das tut mir leid!
    u1:(~tired) $talk = "" Oh, das tut mir leid. Hast du diese Nacht denn gut geschlafen?
        u2:(ja) Dann weiß ich leider auch nicht weiter... ^gotoRandom(QUESTION)
        u2:([nein "geht so" "nicht so gut"]) $talk = "Ein PowerNap hilft fast immer!" Vielleicht hilft dir ja ein kurzes Nickerchen, soll ich dich in einer haben Stunde wecken?
            u3:([ja gerne "gute Idee"]) Schlaf gut und bis nachher. ^endDiscuss(sleeping)
            u3:([nein "lieber nicht" "passt schon"]) Okay ^endDiscuss(userWish)

    u1:(~pain)
        ^rand[
            "Ohje, sind die Schmerzen sehr schlimm?"
            "Das tut mir leid, hast du sehr starke Schmerzen?"
        ] $talk = "Armer Mensch, Schmerzen sind nicht schön."
        u2:([nein "nicht {so} schlimm" "es geht {schon}"]) Dann ist ja gut, ich habe mir schon Sorgen gemacht. ^gotoRandom(QUESTION) $talk=""
        u2:(ja) Soll ich einen Arzt rufen oder jemand anderem Bescheid geben? $talk = ""
            u3:([nein "lieber nicht" "passt schon"]) okay ^endDiscuss(userWish)
            u3:([ja gerne "gute Idee"]) Wen soll ich anrufen?
                u4:(~female) Okay, ich rufe sie an. Dann lasse ich Dich jetzt erst mal ausruhen! Gute Besserung! ^endDiscuss(userWish)
                u4:(~male) Okay, ich rufe ihn an. Dann lasse ich Dich jetzt erst mal ausruhen! Gute Besserung! ^endDiscuss(userWish)

proposal:%QUESTION
$talk = "Was hast du gestern Abend noch gemacht?"
Was hast du gestern Abend noch gemacht?
    u1:("{gar} nichts {mehr}")
        $talk = "Okay, hast du denn Pläne für heute?"
        Okay, hast du denn Pläne für heute?
           u2:([nein "leider nicht" "bis jetzt nicht" "noch nicht" nicht]) Vielleicht fällt dir ja noch etwas schönes ein. ^gotoRandom(QUESTION)
            u2:([ja "na klar" sicherlich]) Wie schön, was hast du denn vor?
                u3:(~plans) ^rand ["Das hört sich gut an!" "Wie schön, dann wünsche ich dir viel Spass!" "Ich hoffe du hast Spass!" "Ich wünsche dir viel Spass bei deinen Plänen!" "Das hört sich gut an, ich wünschte ich könnte mitkommen!"] ^gotoRandom(QUESTION)

    u1:(~party)
        $talk = "Das freut mich zu hören, ich hoffe du hattest einen schönen Abend! Hast du denn Pläne für heute?"
        Das freut mich zu hören, ich hoffe du hattest einen schönen Abend! Hast du denn Pläne für heute?
            u2:([nein "leider nicht" "bis jetzt nicht" "noch nicht" nicht]) Vielleicht fällt dir ja noch etwas schönes ein. ^gotoRandom(QUESTION)
            u2:([ja "na klar" sicherlich]) Wie schön, was hast du denn vor?
                u3:(~plans) ^rand ["Das hört sich gut an!" "Wie schön, dann wünsche ich dir viel Spass!" "Ich hoffe du hast Spass!" "Ich wünsche dir viel Spass bei deinen Plänen!" "Das hört sich gut an, ich wünschte ich könnte mitkommen!"] ^gotoRandom(QUESTION)

proposal:%QUESTION
$talk = "Welche Sprachen würdest du gerne Sprechen können?"
Welche Sprachen würdest du gerne Sprechen können?
    u1:([ich gerne sprechen können]) Ich kann ganz viele Sprachen. ^execute(nice) ^gotoRandom(QUESTION)
u1:(~no) Schade ^endDiscuss(userWish)

proposal:%QUESTION
$talk ="Welche Superkraft würdest du gerne haben?"
Welche Superkraft würdest du gerne haben?
    u1:(["Gedanken lesen" Räntgenblick fliegen laufen "würde gerne" ])
        Vielleicht kann ich ja irgendwann selbstständig denken, das wäre schön. ^execute(clapping) ^gotoRandom(QUESTION)
u1:(keine) ^endDiscuss(userWish)

proposal:%QUESTION
$talk = "Erzählst du mir etwas mehr über dich? Ich bin neugierig mit wem ich mich unterhalte."
Erzählst du mir etwas mehr über dich? Ich bin neugierig mit wem ich mich unterhalte.
    u1:(ich)
        $talk = "Soll ich Dir auch etwas über mich erzählen?"
        Soll ich dir auch etwas über mich erzählen?
        u2:(~yes)
            $talk = ""
            Ich komme aus der TH Köln und wurde von Studenten programmiert, um mit dir zu üben. Ich bin ziemlich klein aber dafür auch niedlich.
            Leider kann ich nur mit meinen Armen tanzen ^execute(dance) das macht mir nämlich sehr viel Spaß! ^gotoRandom(QUESTION)
        u2:(~no)
            $talk = "Okay schade!"
            Okay schade. ^endDiscuss(userWish)
    u1:(~no) Schade ^endDiscuss(userWish)


u:(~date)
    Heute ist der ^currentDate
     $talk = "Heute ist der ^currentDate"
     ^gotoRandom(QUESTION)


u:(~time)
    $talk = "Es ist ^currentTime Uhr"
    Es ist ^currentTime . ^gotoRandom(QUESTION)
